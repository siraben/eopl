[ Generate a range of numbers ]
letrec range(start) =
  proc (stop)
    if
      =(start,stop)
    then
      emptylist
    else
      let step =
        if <(start,stop)
          then
            1
          else
            -1
      in
        cons(start, ((range +(start, step)) stop))

[ Higher order function filter ]    
filter(p) =
  proc (l)
    if null?(l)
    then emptylist
    else if (p car(l)) then
      cons(car(l), ((filter p) cdr(l)))
      else ((filter p) cdr(l))

[ Modular arithmetic ]
mod(x) =
  proc(y)
    let q = /(x,y) in
      let a = *(y, q) in
        -(x, a)

[ Is n divisible by b? ]
dividesq(n) = proc(d) zero?(((mod n) d))

[ Prime loop ]

primeq(n) =
  if =(n, 1)
    then false
  else
    letrec primeloop(d) =
      if <(n, *(d,d))
      then true
      else if ((dividesq n) d)
           then false
           else (primeloop +(d, 1))
      in
    (primeloop 2)
 in
((filter primeq) ((range 1) 100))
